configurations {
    libraries
    // This will make sure that all dependencies that you add to the libraries configuration will also be added to the implementation configuration
    // This way, you only need one dependency declaration for both runtime and compile dependencies
    implementation.extendsFrom libraries
}

java {
    registerFeature("jei") {
        usingSourceSet(sourceSets.main)
    }
    registerFeature("rei") {
        usingSourceSet(sourceSets.main)
    }
    registerFeature("emi") {
        usingSourceSet(sourceSets.main)
    }
    registerFeature("jade") {
        usingSourceSet(sourceSets.main)
    }
    registerFeature("top") {
        usingSourceSet(sourceSets.main)
    }
    registerFeature("kjs") {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    implementation(forge.neoForge)

    // Configuration
    api(forge.configuration)
    jarJar(forge.configuration)

    // JEI
    jeiCompileOnly(forge.jei.common.api) { transitive = false }
    jeiCompileOnly(forge.jei.forge.api) { transitive = false }
    jeiCompileOnly(forge.jei.forge.impl) { transitive = false }

    // REI
    reiCompileOnlyApi(forge.rei.plugin)
    reiCompileOnlyApi(forge.rei.api)
    reiCompileOnly(forge.rei.forge)

    // EMI
    emiCompileOnlyApi("dev.emi:emi-neoforge:${forge.versions.emi.get()}:api")
    emiImplementation(forge.emi)

    // Jade
    jadeImplementation(forge.jade)

    // TOP
    topCompileOnlyApi(forge.theoneprobe) { transitive = false }

    // KJS
    kjsImplementation(forge.kubejs)

    libraries("io.netty:netty-codec-http:4.1.97.Final")
    jarJar("io.netty:netty-codec-http:4.1.97.Final")

    libraries("com.google.zxing:core:3.5.3")
    jarJar("com.google.zxing:core:3.5.3")

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    if (System.getenv("RUN_SERVER") == null) {
        runtimeOnly("maven.modrinth:embeddium:1.0.0-beta.1+mc1.21")
    }

    runtimeOnly("curse.maven:probejs-585406:5492036")
}
